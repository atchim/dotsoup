#!/usr/bin/env fish

set -l usage 'launch
Launch a polybar bar.

Usage
  launch [-h|--help] bar

Flags
  -h, --help  Print help information and exit.

Positional Arguments
  bar  The bar name. The name must match a file located at "~/.config/polybar"
       without the ".ini" extension.'

argparse -n launch -N 1 -X 1 h/help -- $argv
or exit 1

if set -lq _flag_help
  echo $usage
  exit
end

if not test -f "$HOME/.config/polybar/$argv[1].ini"
  echo "launch: There is no bar with name '$argv[1]'" >&2
  exit 1
end

set -l BAR polybar-$USER-$argv[1]
set -l MQUEUE (string replace -r '.*\.' '' (readlink "/tmp/$BAR"))

kill $MQUEUE
polybar -c "$HOME/.config/polybar/$argv[1].ini" "$argv[1]" &
ln -fs "/tmp/polybar_mqueue."(jobs -lp) "/tmp/$BAR"
